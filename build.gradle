plugins {
    id 'java'
    id 'maven'
    id 'signing'
    id 'io.franzbecker.gradle-lombok' version '1.14'
}

group 'se.deadlock'
version '0.0.1-SNAPSHOT'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    maven {
        url 'https://dl.bintray.com/palantir/releases'
    }
}

dependencies {
    compile 'com.palantir.docker.compose:docker-compose-rule-junit4:0.34.0'
    compile 'org.junit.jupiter:junit-jupiter-api:5.3.1'
    
    testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.3.1'
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}

lombok {
    version = "1.18.2"
    sha256 = ""
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

artifacts {
    archives javadocJar, sourcesJar
}

if (project.hasProperty('SONATYPE_USERNAME')) {
    ext."signing.keyId" = SIGNING_KEY_ID
    ext."signing.secretKeyRingFile" = SIGNING_RING_FILE
    ext."signing.password" = SIGNING_PASSWORD

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: SONATYPE_USERNAME, password: SONATYPE_PASSWORD)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: SONATYPE_USERNAME, password: SONATYPE_PASSWORD)
                }

                pom.project {
                    name 'Composed'
                    packaging 'jar'
                    // optionally artifactId can be defined here 
                    description 'Thin junit5 wrapper around https://github.com/palantir/docker-compose-rule'
                    url 'https://github.com/totalorder/composed'

                    scm {
                        connection 'scm:git:git@github.com:totalorder/composed.git'
                        developerConnection 'scm:git:git@github.com:totalorder/composed.git'
                        url 'https://github.com/totalorder/composed'
                    }

                    licenses {
                        license {
                            name 'MIT License'
                            url 'https://github.com/totalorder/composed/blob/master/LICENSE'
                        }
                    }

                    developers {
                        developer {
                            id 'totalorder'
                            name 'Anton Blomberg'
                            email 'anton@deadlock.se'
                        }
                    }
                }
            }
        }
    }
}